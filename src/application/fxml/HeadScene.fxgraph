package application.fxml

import application.Customer
import application.HeadSceneController
import javafx.scene.layout.Pane
import javafx.scene.layout.HBox
import javafx.scene.control.Label
import javafx.scene.control.TabPane
import javafx.scene.control.Tab
import javafx.scene.layout.BorderPane
import javafx.scene.control.TableView
import javafx.scene.control.TableColumn
import javafx.scene.layout.VBox
import javafx.scene.layout.Region
import java.lang.Double
import javafx.scene.control.Button
import javafx.scene.control.cell.*
import javafx.collections.*
import javafx.scene.layout.StackPane
import javafx.scene.layout.GridPane
import javafx.scene.layout.Border
import javafx.geometry.Insets
import javafx.scene.control.TextField

component Sample controlledby application.HeadSceneController {
	VBox {
		children : [
			TabPane id tabPane {
				tabs : [
					Tab id tabList {
						text : "All Customers",
						closable : false,
						content : BorderPane {
							center : TableView < Customer > id tableView {
								columns : [
									TableColumn < Customer, Integer > id idTableColumn {
										text : "Id",
										cellValueFactory : PropertyValueFactory < Customer, Integer > {
											property : "id"
										}
									},
									TableColumn < Customer, String > {
										text : "Family name",
										cellValueFactory : PropertyValueFactory < Customer, String > {
											property : "familyName"
										}
									},
									TableColumn < Customer, String > {
										text : "First name",
										cellValueFactory : PropertyValueFactory < Customer, String > {
											property : "firstName"
										}
									},
									TableColumn < Customer, String > {
										text : "ZIP code",
										cellValueFactory : PropertyValueFactory < Customer, String > {
											property : "zipCode"
										}
									},
									TableColumn < Customer, String > {
										text : "City",
										cellValueFactory : PropertyValueFactory < Customer, String > {
											property : "city"
										}
									},
									TableColumn < Customer, String > {
										text : "Street",
										cellValueFactory : PropertyValueFactory < Customer, String > {
											property : "street"
										}
									},
									TableColumn < Customer, String > id houseNumberTableColumn {
										text : "House number",
										cellValueFactory : PropertyValueFactory < Customer, String > {
											property : "houseNumber"
										}
									}

								]
							},
							bottom : HBox {
								Region {
									prefWidth : 2000
								},
								VBox {
									children : [
										Button {
											text : "Add Customer",
											minWidth : 200,
											onMouseClicked : controllermethod handleAdd
										},
										Button {
											text : "Modify Customer",
											minWidth : 200,
											onMouseClicked : controllermethod handleModify
										},
										Button {
											text : "Delete Customer",
											minWidth : 200,
											onMouseClicked : controllermethod handleDelete
										}
									]
								}
							}
						}
					},
					Tab id tabDetail {
						text : "Customer Details",
						closable : false,
						content : Pane id detailsPane {
							children : [
								BorderPane id detailsBorderPane {
									center : GridPane {
										styleClass : [
											"border"
										],
										vgap : 3,
										children : [
										// Left side
											Label {
												text : "Family name:",
												textAlignment : "RIGHT",
												static halignment : "RIGHT",
												padding : Insets {
													right : 5
												},
												static rowIndex : 0,
												static columnIndex : 0
											},
											Label {
												text : "Street:",
												textAlignment : "RIGHT",
												static halignment : "RIGHT",
												padding : Insets {
													right : 5
												},
												static rowIndex : 1,
												static columnIndex : 0
											},
											Label {
												text : "City:",
												textAlignment : "RIGHT",
												static halignment : "RIGHT",
												padding : Insets {
													right : 5
												},
												static rowIndex : 2,
												static columnIndex : 0
											},
											TextField id familyNameField {
												promptText : "Mustermann",
												static rowIndex : 0,
												static columnIndex : 1
											},
											TextField id streetField {
												promptText : "Musterstraße",
												static rowIndex : 1,
												static columnIndex : 1
											},
											TextField id cityField {
												promptText : "München",
												static rowIndex : 2,
												static columnIndex : 1
											},
											// Right side
											Label {
												text : "First name:",
												textAlignment : "RIGHT",
												static halignment : "RIGHT",
												padding : Insets {
													left : 40,
													right : 5
												},
												static rowIndex : 0,
												static columnIndex : 3
											},
											Label {
												text : "House number:",
												textAlignment : "RIGHT",
												static halignment : "RIGHT",
												padding : Insets {
													left : 40,
													right : 5
												},
												static rowIndex : 1,
												static columnIndex : 3
											},
											Label {
												text : "ZIP code:",
												textAlignment : "RIGHT",
												static halignment : "RIGHT",
												padding : Insets {
													left : 40,
													right : 5
												},
												static rowIndex : 2,
												static columnIndex : 3
											},
											TextField id firstNameField {
												promptText : "Max",
												static rowIndex : 0,
												static columnIndex : 4
											},
											TextField id houseNumberField {
												promptText : "2b",
												static rowIndex : 1,
												static columnIndex : 4
											},
											TextField id zipCodeField {
												promptText : "85521",
												static rowIndex : 2,
												static columnIndex : 4
											}
										]
									},
									bottom : HBox {
										styleClass : [
											"borderBottonBelow"
										],
										Region {
											prefWidth : 2000
										},
										HBox {
											children : [
												Button {
													text : "Cancel",
													minWidth : 110,
													onMouseClicked : controllermethod handleCancle
												},
												Button {
													text : "Reset Details",
													minWidth : 110,
													onMouseClicked : controllermethod handleReset
												},
												Button id saveDetails {
													text : "Save Details",
													minWidth : 110,
													disable : true,
													onMouseClicked : controllermethod handleSave
												}
											]
										}
									}
								},
								HBox {
									styleClass : [
										"borderBox"
									],
									children : [
										Label {
											text : "Customer details",
											styleClass : [
												"borderText"
											]
										}
									]
								}
							]
						}
					}
				]
			}
		]
	}
}